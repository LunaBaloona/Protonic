# Maintainer: LunaBaloona
pkgname=protonic
pkgver=0.2.1
pkgrel=1
pkgdesc="GUI application for launching Windows executables inside Steam Proton environments"
arch=('x86_64')
url="https://github.com/LunaBaloona/protonic"
license=('GPL2')
depends=('gcc-libs' 'steam')
makedepends=('cargo' 'git')
optdepends=('python: required for protonhax')
source=(
    "git+https://github.com/LunaBaloona/protonic.git#tag=v${pkgver}"
    "protonhax::git+https://github.com/jcnils/protonhax.git"
)
sha256sums=('SKIP' 'SKIP')

prepare() {
    cd "$srcdir/protonic"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$srcdir/protonic"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd "$srcdir/protonic"
    
    # Install protonic binary
    install -Dm755 "target/release/protonic" "$pkgdir/usr/bin/protonic"
    
    # Install protonhax
    install -Dm755 "$srcdir/protonhax/protonhax" "$pkgdir/usr/bin/protonhax"
    
    # Install desktop file
    install -Dm644 "$srcdir/protonic/packaging/protonic.desktop" "$pkgdir/usr/share/applications/protonic.desktop"
    
    # Install icon
    install -Dm644 "$srcdir/protonic/ui/icon.png" "$pkgdir/usr/share/icons/hicolor/256x256/apps/protonic.png"
    
    # Install license
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
